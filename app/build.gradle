apply plugin: 'com.android.model.application'

// Root of 3rd party lib(s): location could be anywhere on the host system
def lib_distribution_root = '../externals'
model {
    repositories {
        libs(PrebuiltLibraries) {
            // Configure one pre-built lib: shared
            iconv {
                // Inform Android Studio where header file dir for this lib
                headers.srcDir "${lib_distribution_root}/iconv/include"
                // Inform Android Studio where lib is -- each ABI should have a lib file
                binaries.withType(SharedLibraryBinary) {
                    sharedLibraryFile = file("${lib_distribution_root}/iconv/lib/${targetPlatform.getName()}/libiconv.so")
                }
            }

            soundtouch {
                // Inform Android Studio where header file dir for this lib
                headers.srcDir "${lib_distribution_root}/soundtouch/include"
                // Inform Android Studio where lib is -- each ABI should have a lib file
                binaries.withType(SharedLibraryBinary) {
                    sharedLibraryFile = file("${lib_distribution_root}/soundtouch/libs/${targetPlatform.getName()}/libsoundtouch.so")
                }
            }
        }
    }
    android {
        compileSdkVersion 23
        buildToolsVersion "23.0.3"

        defaultConfig {
            applicationId "org.citraemu.citra"
            minSdkVersion.apiLevel 23
            targetSdkVersion.apiLevel 23
            versionCode 1
            versionName "1.0"
        }

        ndk {
            moduleName "Citra"
            toolchain "clang"
            stl "gnustl_static"
            ldLibs.addAll(["atomic", "log", "GLESv2"])
            cppFlags.addAll(['-std=c++14',
                             '-I' + file('src/main/jni'),
                             '-I' + file('src/main/jni/common'),
                             '-I' + file('src/main/jni/common/logging')])
        }

        sources {
            main {
                jni {
                    dependencies {
                        library "iconv" linkage "shared"
                        library "soundtouch" linkage "shared"
                    }
                }
                jniLibs {
                    // for shared lib, lib need to be pushed to the target too
                    // Once libs are copied into app/src/main/jniLibs directory,
                    // Android Studio will pack them into APK's lib/ directory
                    // Here we like to avoid another duplication by pointing
                    // to the files that containing our libs' distribution location
                    // so the same file is used by compiler at host, also packed
                    // into APk to be used at Target (phone/tablet)
                    source {
                        srcDir "${lib_distribution_root}/iconv/lib"
                        srcDir "${lib_distribution_root}/soundtouch/lib"
                    }
                }
            }
        }

        buildTypes {
            debug {
                debuggable true
            }
            release {
                minifyEnabled false
                proguardFiles.add(file('proguard-rules-pro'))
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.google.android.gms:play-services-ads:9.0.0'
    compile 'com.github.bluejamesbond:textjustify-android:2.1.6'
}
